import de.qualersoft.jmeter.gradleplugin.task.JMeterRunTask

buildscript {
	ext {
		springBootVersion = '3.2.7'
		lombokVersion = '1.18.26'
		mapStructVersion = '1.3.1.Final'
		postgresDriverVersion = '42.6.2'
		liquibaseVersion = '4.24.0'
		openAPIVersion = '2.0.2'
		hibernateValidatorVersion = '8.0.0.Final'
		h2Version = '2.2.224'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.7'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'de.qualersoft.jmeter' version '2.4.0'
}

group = 'com.book'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	integrationTestRuntime.extendsFrom testRuntimeOnly
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
	integrationTestImplementation.extendsFrom testImplementation
}

repositories {
	mavenCentral()
}

jmeter {
	// basic config
	jmxRootDir = file('src/test/jmeter')
	resultDir = file("$buildDir/jmeter/results")
	reportDir = file("$buildDir/jmeter/reports")
}

sourceSets {
	integrationTest {
		java {
			srcDir file('src/integration-test/java')

			compileClasspath += main.output + test.output + main.compileClasspath + test.compileClasspath
			runtimeClasspath += main.output + test.output + main.runtimeClasspath + test.runtimeClasspath
		}
	}
}

dependencies {

	// Spring dependencies
	implementation("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
	implementation("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
	implementation("org.liquibase:liquibase-core:$liquibaseVersion")
	implementation("org.hibernate.validator:hibernate-validator:$hibernateValidatorVersion")

	// Persistence
	runtimeOnly("org.postgresql:postgresql:$postgresDriverVersion")

	// Annotation processors
	implementation("org.mapstruct:mapstruct:$mapStructVersion")
	compileOnly("org.mapstruct:mapstruct-processor:$mapStructVersion")
	compileOnly("org.projectlombok:lombok:$lombokVersion")
	annotationProcessor(
			"org.projectlombok:lombok:$lombokVersion",
			"org.mapstruct:mapstruct-processor:$mapStructVersion"
	)

	// Tools
	implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:$openAPIVersion")

	// Tests
	testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly("com.h2database:h2:$h2Version")
}

tasks.register('runJMeter', JMeterRunTask) {
	jmxFile.set("${projectDir}/src/test/jmeter/load_test.jmx")
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
	useJUnitPlatform()

	description = "Runs the integration tests"
	group = "verification"
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	maxHeapSize = '1024m'
}
